; z80float is a floating point library targeting the Z80 processor. While this
; project is tested on a TI-84+, the code should be generic enough to run on any
; Z80 CPU with very little modification (mostly defining scrap RAM locations).
;
; adapted by Marco Spedaletti

FPSINGLE_MULU8_DIVPOW2:
	CALL FPPUSHPOP
	PUSH BC
	PUSH DE
	LD C, D

	LD E, (HL)
	INC HL
	LD D, (HL)
	INC HL
	LD A, (HL)
	ADD A, A
	LD B, A
	INC HL
	LD A, (HL)
	ADC A, A
	JR Z, FPSINGLE_MULU8_DIVPOW2_ZERO
	INC A
	JR Z, FPSINGLE_MULU8_DIVPOW2_INFNAN
	; CHECK IF WE ARE MULTIPLYING BY 0!
	INC C
	DEC C
	JR Z, FPSINGLE_MULU8_DIVPOW2_ZERO

	POP HL	;
	PUSH AF

	;EXTEND THE SIGN OF L INTO H
	LD A, L
	ADD A, A
	SBC A, A
	LD H, A
	PUSH HL

	; B IS SHIFTED OVER BY 2; WE STILL NEED TO SHIFT A 1 BACK IN
	SCF
	RR B
	CALL FP_C_TIMES_BDE	;CAHL
	LD B, A
	POP DE	;DE IS HOW MUCH TO ADD TO THE EXPONENT
	POP AF	; A IS THE CURRENT EXPONENT, CARRY IS SIGN
	PUSH AF ; SAVE CARRY

;CBHL
	;NEED TO ADD E+7 TO THE EXPONENT, AND SHIFT CBHL UP
	ADD A, E
	JR NC, $+3
	INC D

	ADD A, 7
	JR NC, $+3
	INC D

	INC D
	JR Z, FPSINGLE_MULU8_DIVPOW2_ZERO2
	DEC D
	JR NZ, FPSINGLE_MULU8_DIVPOW2_INF2

	LD E, A
	INC C
	DEC C
	JR FPSINGLE_MULU8_DIVPOW2_ADJUST
FPSINGLE_MULU8_DIVPOW2_LOOP:
	DEC DE
	ADD HL, HL
	RL B
	RL C
FPSINGLE_MULU8_DIVPOW2_ADJUST:
	JP P, FPSINGLE_MULU8_DIVPOW2_LOOP
	SLA C
	POP AF
	LD A, E
	LD E, H
	LD D, B
	LD B, C
	DEFB $21 ;START OF `LD HL,**` TO EATT THE NEXT 2 BYTES (DEC A \ POP DE)
FPSINGLE_MULU8_DIVPOW2_INFNAN:
	DEC A
FPSINGLE_MULU8_DIVPOW2_ZERO:
	POP DE
FPSINGLE_MULU8_DIVPOW2_RETURN:
; SIGN IS IN CARRY, A IS THE EXPONENT, (B/2)DE IS THE SIGNIFICAND
	RRA
	RR B
	POP HL
	LD (HL), E
	INC HL
	LD (HL), D
	INC HL
	LD (HL), B
	INC HL
	LD (HL), A
	RET
FPSINGLE_MULU8_DIVPOW2_INF2:
	LD A, -1
	DEFB $FE
FPSINGLE_MULU8_DIVPOW2_ZERO2:
	XOR A
	LD DE, 0
	LD B, D
	JR FPSINGLE_MULU8_DIVPOW2_RETURN
